plugins {
    id 'java'
    id 'idea'
    id 'com.diffplug.spotless' version '5.5.1'
    id 'com.github.spotbugs' version '4.0.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    // https://gitlab.com/barfuin/gradle-taskinfo
    // Gradle plugin that displays task dependencies and types
    id 'org.barfuin.gradle.taskinfo' version '1.3.0'
}
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
group 'bio.terra'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    invoker
}

// $app corresponds to the name of the directory that contains application-specific code
// src/main/java/cloudfunctions/$app
def app = project.hasProperty("application") ? application : "testrunner"

sourceSets {
    main {
        java {
            includes = ["bio/terra/cloudevents/**", "bio/terra/cloudfunctions/common/**", "bio/terra/cloudfunctions/utils/**", "bio/terra/cloudfunctions/$app/**"]
        }
    }
}

dependencies {
    ext {
        googleFunctions = '1.0.4'
        googleEvents = '0.3.0'
        googleFunctionsInvoker = '1.0.2'
        googleBigQuery = '1.131.1'
        googleStorage = '1.115.0'
        apacheCompress = "1.20"
        apacheLang3 = "3.12.0"
        gson = "2.8.7"
        guava = "30.1.1-jre"
        slf4j = "1.7.30"
    }

    implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4j}"

    // Gson for Java serialization/deserialization
    implementation group: 'com.google.code.gson', name: 'gson', version: "${gson}"

    // Apache Commons Lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: "${apacheLang3}"

    // To decompress .gz, .tar.gz
    implementation group: 'org.apache.commons', name: 'commons-compress', version: "${apacheCompress}"

    // Google Core Libraries For Java
    implementation group: 'com.google.guava', name: 'guava', version: "${guava}"

    // Every function needs this dependency to get the Functions Framework API.
    implementation group: 'com.google.cloud.functions', name: 'functions-framework-api', version: "${googleFunctions}"

    // Types in Java for CloudEvents issued by Google.
    implementation group: 'com.google.cloud', name: 'google-cloudevent-types', version: "${googleEvents}"

    // Google Cloud SDK needed for Cloud Storage
    implementation group: 'com.google.cloud', name: 'google-cloud-storage', version: "${googleStorage}"

    // Google Cloud SDK needed for Streaming BQ
    implementation group: 'com.google.cloud', name: 'google-cloud-bigquery', version: "${googleBigQuery}"

    // To run function locally using Functions Framework's local invoker
    invoker group: 'com.google.cloud.functions.invoker', name: 'java-function-invoker', version: "${googleFunctionsInvoker}"

    // These dependencies are only used by the tests.
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// ./gradlew printSourceSetInformation prints sourceSets properties
task printSourceSetInformation() {
    doLast{
        sourceSets.each { srcSet ->
            println "["+srcSet.name+"]"
            print "-->Source directories: "+srcSet.allJava.srcDirs+"\n"
            print "-->Output directories: "+srcSet.output.classesDirs.files+"\n"
            print "-->Compile classpath:\n"
            srcSet.compileClasspath.files.each {
                print "  "+it.path+"\n"
            }
            println ""
        }
    }
}

// For more info, please refer to https://cloud.google.com/functions/docs/first-java#gradle_1
// Register a "runFunction" task to run the function locally
tasks.register("runFunction", JavaExec) {
    mainClass = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            '--target', project.findProperty('run.functionTarget') ?: '',
            '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

// Deploy cloud function from a JAR
// Build and deploy an Uber JAR
// https://cloud.google.com/functions/docs/concepts/java-deploy#gradle
// USAGE: .gradlew -Papplication=$app shadowJar
// $app = testrunner, deltalayer etc
shadowJar {
    mergeServiceFiles()
}

compileJava.dependsOn spotlessApply
shadowJar.dependsOn printSourceSetInformation
